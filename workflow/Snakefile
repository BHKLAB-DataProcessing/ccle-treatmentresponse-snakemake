from damply import dirs as dmpdirs

configfile: "config/pipeline.yaml"

CONTAINER = 'docker://ghcr.io/bhklab-dataprocessing/ccle-treatmentresponse-snakemake:16d5068'

storage:
    provider="http"

rule buildTreatmentResponseExperiment:
    input:
        sampleMetadata = dmpdirs.METADATA / "sampleMetadata.tsv",
        treatmentMetadata = dmpdirs.METADATA / "treatmentMetadata.tsv",
        preprocessed_raw = dmpdirs.PROCDATA / "preprocessed_TreatmentResponse_raw.csv",
        preprocessed_profiles = dmpdirs.PROCDATA / "preprocessed_TreatmentResponse_profiles.csv",
    output:
        tre = dmpdirs.RESULTS / "treatmentResponseExperiment.RDS",
    log:
        dmpdirs.LOGS / "buildTreatmentResponseExperiment.log",
    container:
        CONTAINER
    threads:
        40
    script:
        dmpdirs.SCRIPTS / "R" / "buildTreatmentResponseExperiment.R"

rule preprocessMetadata:
    input:
        sampleAnnotation = storage.http(config["metadata"]["sampleAnnotation"]),
        treatmentAnnotation = storage.http(config["metadata"]["treatmentAnnotation"]),
    output:
        sampleMetadata = dmpdirs.METADATA / "sampleMetadata.tsv",
        treatmentMetadata = dmpdirs.METADATA / "treatmentMetadata.tsv",
    container:
        CONTAINER
    script:
        dmpdirs.SCRIPTS / "R" / "preprocessMetadata.R"


rule preprocessTreatmentResponse:
    input:
        rawdata = storage.http(config["treatmentResponse"]["rawdata"]["url"]),
    output:
        preprocessed_raw = dmpdirs.PROCDATA / "preprocessed_TreatmentResponse_raw.csv",
        preprocessed_profiles = dmpdirs.PROCDATA / "preprocessed_TreatmentResponse_profiles.csv",
    log:
        dmpdirs.LOGS / "preprocessTreatmentResponse.log",
    script:
        dmpdirs.SCRIPTS / "R" / "preprocessTreatmentResponse.R"

